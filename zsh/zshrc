# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

export EDITOR=nvim
export VISUAL="nvim -u NONE" # TODO mini nvimrc here just to set up a few convenience maps, maybe targets.vim
export LESS='-g -i -M -R -S -w -z-4'
export PATH=/apollo/env/SDETools/bin:$HOME/dev/idea-IC-141.1532.4/bin:/usr/NX/bin:$HOME/dev:$HOME/dev/dmenu2:$HOME/dev/neovim/build/bin:$HOME/dev/ranger:$HOME/.local/bin:$HOME/dev/calcurse/src:$HOME/dev/local/bin:$HOME/dev/ag:$PATH:$HOME/dotfiles/vim/bundle/fzf/bin

stty -ixon  # stop C-s from activating scroll lock
ulimit -c unlimited

GPG_TTY=$(tty)
export GPG_TTY

for config_file ($HOME/dotfiles/zsh/{*.zsh,plugins/*.zsh}); do
    source $config_file
done
source $HOME/dotfiles/scripts/fzf_utilities.sh
# TODO i only used "main" and "brackets" syntax highlighters, zpreztorc can turn on more

# list of actions here http://www.cs.elte.hu/zsh-manual/zsh_14.html
# bindkey -v    prezto sets vi mode for me
bindkey "^[[3~" delete-char         # make delete key work
bindkey "^[[7~" beginning-of-line   # make home key work
bindkey "^[[8~" end-of-line         # make end key work
bindkey "^[f"   forward-word
bindkey "^[b"   backward-word
bindkey "^[m"   copy-prev-shell-word
bindkey "^A"    beginning-of-line
bindkey "^B"    backward-char
bindkey "^E"    end-of-line
bindkey "^F"    forward-char
bindkey "^G"    delete-char         # C-g deletes character in front of cursor, opposite C-h
bindkey "^H"    backward-delete-char
# bindkey '^I'    expand-or-complete  # expand-or-complete-with-dots by default, but fzf-completion takes it
bindkey "^K"    kill-line
bindkey "^L"    clear-screen
bindkey "^Q"    delete-word         # C-q deletes word in front of cursor, opposite C-w
bindkey "^[r"   history-incremental-search-backward
bindkey "^S"    sudo-command-line   # C-s inserts 'sudo' at beginning of line
bindkey "^U"    kill-whole-line
bindkey "^V"    vi-find-prev-char
bindkey "^W"    backward-delete-word
bindkey "^Y"    undo
bindkey "^_"    vi-find-next-char
bindkey "^@"    vi-repeat-find
bindkey "^?"    backward-delete-char
bindkey '^P'    history-substring-search-up           # bind P and N for EMACS mode history-substring-search
bindkey '^N'    history-substring-search-down
bindkey -M vicmd 'k' history-substring-search-up      # bind k and j for VI mode history-substring-search
bindkey -M vicmd 'j' history-substring-search-down
bindkey -M vicmd 'V' visual-mode                      # prezto binds 'v' to open $VISUAL
KEYTIMEOUT=1

# http://sgeb.io/articles/zsh-zle-closer-look-custom-widgets/
# zle -N <widget name> <function name>
lskeyimpl() {
    if   [ "$WIDGET" = "lskey" ];  then BUFFER="ls"
    elif [ "$WIDGET" = "lslkey" ]; then BUFFER="ls -l"
    fi
    zle accept-line
}
zle -N lskey lskeyimpl
zle -N lslkey lskeyimpl
bindkey '\el' lskey
bindkey '\eL' lslkey

gitstatuskeyimpl() {
    if git rev-parse --git-dir > /dev/null 2>&1; then
        if [ "$WIDGET" = "gitstatuskey" ]; then
            BUFFER="git status --column"
        elif [ "$WIDGET" = "gitstatusshortkey" ]; then
            BUFFER="git status --short"
        fi
        zle accept-line
    fi
}
zle -N gitstatuskey gitstatuskeyimpl
zle -N gitstatusshortkey gitstatuskeyimpl
bindkey '\eg' gitstatuskey
bindkey '\eG' gitstatusshortkey

updirkey() {
    cd .. && zle reset-prompt
}
zle -N updirkey updirkey
bindkey "" updirkey # Ctrl + /

popdkey() {
    if [ "$#dirstack" != "0" ]; then popd && zle reset-prompt; fi
}
zle -N popdkey popdkey
bindkey "" popdkey # Alt + Ctrl + /

if [ -n "$STARTUPDIR" ] && [ -d "$STARTUPDIR" ]; then
    cd $STARTUPDIR
    unset STARTUPDIR
fi
